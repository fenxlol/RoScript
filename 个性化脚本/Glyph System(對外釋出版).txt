///////////////////////////////////////////////////////////////
//                                                           //
// 雕文系統 (Glyph System)                                   //
//                                                           //
// 版本: Ver1.0                                              //
//                                                           //
// 作者: 小鮭魚 (grass0916)                                  //
//                                                           //
// 腳本簡介: 玩家藉由容易、中等、困難的方式取得雕文。        //
//           將它銘文在身上, 使技能有額外的特殊強化效果,     //
//           有限定雕文的使用數量, 玩家們的玩法就可以        //
//           更活、更自由。                                  //
//                                                           //
// 腳本備註: 對外釋出版, 移除極效雕文的部份, 並且只留下      //
//           劍士雕文給下載者參考。                          //
//                                                           //
///////////////////////////////////////////////////////////////

gef_fild07,180,240,4	script	小鮭魚雕文	897,{

	set $NPC_Glyph$, "[小鮭魚雕文]" ;
	switch( select( "^ff0000開發雕文 - 技能強化^000000", "^336600什麼是銘文學 ?^000000" ) ) {
		case 1:
			callfunc "Glyph" ;
			close ;
		case 2:
			callfunc "Guile_Glyph" ;
			close ;
	} // switch

	OnInit:
		// 防止不預期意外發生的初始化
		deletearray $Glyph_Primer ; deletearray $Glyph_Primer_Name$ ;
		// 雕文控制器(被安插在頭飾上的道具)
		set $Glyph_Control, 30000 ;
		// 初階雕文
		setarray $Glyph_Primer[1], 30001, 30002, 30003, 30004, 30005, 30006, 30007, 30008, 30009, 30010, 30011, 30012, 30013, 30014, 30015, 30016, 30017, 30018, 30019, 30020, 30021, 30022, 30023, 30024, 30025, 30026, 30027, 30028, 30029, 30030, 30031, 30032, 30033, 30034, 30035, 30036, 30037, 30038, 30039, 30040, 30041, 30042, 30043, 30044, 30045, 30046, 30047, 30048, 30049, 30050, 30051, 30052, 30053 ;
		setarray $Glyph_Primer_Name$[1], "基本技能雕文", "單手劍使用熟練度雕文", "雙手劍使用熟練度雕文", "快速回復雕文", "狂擊雕文", "挑釁雕文", "怒爆雕文", "霸體雕文", "禪心雕文", "火狩雕文", "火箭術雕文", "冰箭術雕文", "雷擊術雕文", "聖靈召喚雕文", "火球術雕文", "冰凍術雕文", "雷爆術雕文", "石化術雕文", "心靈爆破雕文", "火焰之壁雕文", "暗之障壁雕文", "鶚梟之眼雕文", "蒼鷹之眼雕文", "心神凝聚雕文", "二連矢雕文", "箭雨雕文", "天使之護雕文", "天使之擊雕文", "光獵雕文", "光之障壁雕文", "瞬間移動雕文", "傳送之陣雕文", "治癒術雕文", "加速術雕文", "緩速術雕文", "天使之淚雕文", "天使之光雕文", "天使之障壁雕文", "天使之賜福雕文", "治療術雕文", "負重量上升雕文", "低價買進雕文", "高價賣出雕文", "手推車使用雕文", "物品鑒定雕文", "露天商店雕文", "金錢攻擊雕文", "二刀連擊雕文", "殘影雕文", "偷竊雕文", "隱匿雕文", "施毒雕文", "解毒雕文" ;
		end ;

}

function	script	Glyph	{

	set .@PClevel, callfunc( "Glyph_Condition" ) ; // 依等級開發的雕文數不同

	setarray .@Glyph_Type$[1], "第一初階雕文", "第二初階雕文", "第三初階雕文" ;

	switch( select( "配置雕文", "啟用雕文效果" ) ) {
		case 1:
			mes .@Glyph_Type$[1] + " : ^0066ff" + Glyph_Primer_01$ + "^000000" ;
			mes .@Glyph_Type$[2] + " : ^0066ff" + Glyph_Primer_02$ + "^000000" ;
			mes .@Glyph_Type$[3] + " : ^0066ff" + Glyph_Primer_03$ + "^000000" ;
			next ;
			switch( select( ( .@PClevel >= 1 )? "修改 " + .@Glyph_Type$[1] : "等級不足, 無法開啟 " + .@Glyph_Type$[1], ( .@PClevel >= 2 )? "修改 " + .@Glyph_Type$[2] : "等級不足, 無法開啟 " + .@Glyph_Type$[2], ( .@PClevel >= 3 )? "修改 " + .@Glyph_Type$[3] : "等級不足, 無法開啟 " + .@Glyph_Type$[3] ) ) {
				case 1:
					if ( .@PClevel >= 1 )
						callfunc "Glyph_Set", Glyph_Primer_01$, .@Glyph_Type$[1], 1 ;
					break ;
				case 2:
					if ( .@PClevel >= 2 )
						callfunc "Glyph_Set", Glyph_Primer_02$, .@Glyph_Type$[2], 1 ;
					break ;
				case 3:
					if ( .@PClevel >= 3 )
						callfunc "Glyph_Set", Glyph_Primer_03$, .@Glyph_Type$[3], 1 ;
					break ;
			}

			close ;

		case 2:
			callfunc "Glyph_TurnOn" ;
			close ;
	} // switch

}


function	script	Glyph_Condition	{

	else if ( Upper ) return 4 ;
	else if ( !Upper && BaseLevel >= 90 ) return 3 ;
	else if ( !Upper && BaseLevel >= 60 ) return 2 ;
	else if ( !Upper && BaseLevel >= 30 ) return 1 ;
	else return 0 ;

}

function	script	Glyph_Set	{

	if ( getarg(2) == 1 ) {
		copyarray .@level, $Glyph_Primer, getarraysize( $Glyph_Primer )  ;
		copyarray .@level_Name$, $Glyph_Primer_Name$, getarraysize( $Glyph_Primer_Name$ ) ;
		set .@level2$, "$Glyph_Primer" ;
		set .@level_Name2$, "$Glyph_Primer_Name$" ;
	} // if

	mes $NPC_Glyph$ ;
	mes "請輸入你要學習的雕文編號的 0~ 100。" ;
	mes "^ff0000  輸入 999, 即可遺忘掉該雕文。^000000" ;
	next ;
	input .@select ;

	// 數字 0, 跳出雕文銘文選單
	if ( .@select == 0 )
		close ;

	// 數字 999, 遺忘該雕文
	if ( .@select == 999 ) {
		if ( select( "取消", "^ff0000遺忘雕文^000000" ) == 2 ) {
			specialeffect2 618 ;
			set getarg(0), "" ;
			dispbottom "已經遺忘了雕文 ..." ;
		} // if
		return ;
	} // if

	// 超出範圍的數字, 會要你一直輸入到正確範圍內為止
	if ( .@select < 0 || .@select > getarraysize( .@level ) - 1 )
		callfunc "Glyph_Set", getarg(0), getarg(1), getarg(2) ;
	else {
		if ( countitem( .@level[ .@select ] ) ) {
			if ( callfunc( "Glyph_Set_Check", getarg(1), getd( .@level_Name2$ + "[" + .@select + "]" ), getd( .@level2$ + "[" + .@select + "]" ) ) )
				set getarg(0), .@level_Name$[ .@select ] ;
		} // if
		else {
			specialeffect2 611 ;
			mes $NPC_Glyph$ ;
			mes "你身上並沒有 ^ff0000" + .@level_Name$[ .@select ] + " ^000000。" ;
			close ;
		} // else
	} // else
	return ;

}

function	script	Glyph_Set_Check	{

	if ( callfunc( "Glyph_Set_Check_Duplicate", getarg(1) ) ) {
		specialeffect2 611 ;
		mes $NPC_Glyph$ ;
		mes "該雕文已經存在, 不可重複銘文。" ;
		close;
	} // if

	mes $NPC_Glyph$ ;
	mes "確定將 ^ff0000" + getarg(1) + "^000000 銘文在 ^0033ff" + getarg(0) + "^000000 上嗎 ?" ;
	if ( select( "取消", "確定" ) == 2 ) {
		specialeffect2 408 ; specialeffect2 610 ;
		delitem getarg(2), 1 ;
		dispbottom getarg(1) + " 已經成功銘文在 " + getarg(0) + "。" ;
		return 1 ;
	} // if
	return 0 ;

}

function	script	Glyph_Set_Check_Duplicate	{

	for ( set .@i, 1 ; .@i <= 3 ; set .@i, .@i + 1 ) {
		if ( getd( "Glyph_Primer_0" + .@i + "$" ) == getarg(0) )
			return 1 ;
	} // for
	return 0 ;

}

function	script	Glyph_TurnOn	{

	if ( getequipid( 9 ) == -1 ) {
		mes $NPC_Glyph$ ;
		mes "請裝備^ff0000頭中^000000的頭飾。" ;
		mes "我將會將雕文魔法能量注入其中。" ;
		close ;
	} // if

	else {
		query_sql "SELECT `refine`, `card0`, `card1`, `card2`, `card3` From `inventory` WHERE `char_id` = " + getcharid(0) + " AND `equip` = 512", .@temprefine, .@tempcard1, .@tempcard2, .@tempcard3, .@tempcard4 ;
		mes $NPC_Glyph$ ;
		mes "同一種 Head-2 ^ff0000請勿攜帶兩個以上^000000" ;
		mes "不在預期中被刪除道具" ;
		mes "^ff0000恕 不 賠 償^000000" ;
		next ;
		set .@tempHead2, getequipid( 9 ) ;
		mes $NPC_Glyph$ ;
		mes "請確認你目前的 Head-2 的是否為: " ;
		mes "//-----------------------------//" ;
		mes "     " + getequipname( 9 ) + " 精練值: +" + .@temprefine ;
		mes "     安插的卡片為 : "+ getitemname( .@tempcard1 ) +"" ;
		mes "//-----------------------------//" ;
		mes "若不是, 請重新登入後會自動刷新。" ;
		next ;
		if ( countitem( .@tempHead2 ) > 1 ) {
			mes $NPC_Glyph$ ;
			mes "你攜帶兩個以上的相同的 Head-2 裝備, " ;
			mes "將不需要付予雕文能力的裝備放入倉庫。 " ;
			close ;
		} // if
		if ( select( "取消", "沒有錯, 我要進行啟用銘文" ) == 2 ) {
			mes $NPC_Glyph$ ;
			mes "現在將雕文之力注入你的體內 ..." ;
			specialeffect2 805 ;
			delitem .@tempHead2, 1 ;
			getitem2 .@tempHead2, 1, 1, .@temprefine, 0, .@tempcard1, .@tempcard2, .@tempcard3, $Glyph_Control ;
		} // if
		close ;
	} // else

}

function	script	Guile_Glyph	{

	mes $NPC_Glyph$ ;
	mes "銘文學, 是由巫師們將魔法能量注入在雕文上, " ;
	mes "而這股魔法能量可以激發人們的潛能, " ;
	mes "對於技能的使用可以更容易抓住其精隨。" ;
	next ;
	mes $NPC_Glyph$ ;
	mes "而你只需要帶來銘文師撰寫的雕文, " ;
	mes "我能引導你閱讀其內容, 加速瞭解技能的奧義。" ;
	next ;
	callsub Teaching ;

	Teaching:
		mes $NPC_Glyph$ ;
		mes "還有其他問題嗎 ?" ;
		next ;

		switch( select( "如何取得雕文 ?", "雕文開發的等級限制 ?", "雕文的效果 ?", "雕文的職業限制 ?" ) ) {
			case 1:
				mes $NPC_Glyph$ ;
				mes "雕文是高深的巫師將魔力銘文在符紙上, " ;
				mes "而銘文紙材料必須藉由生活採集: " ;
				mes "伐木、釣魚, 來取得製作。" ;
				next ;
				mes $NPC_Glyph$ ;
				mes "大部分的初階雕文 NPC都有販售, " ;
				mes "而極效雕文必須有巫師製作。 " ;
				next ;
				callsub Teaching ;
				close ;
			case 2:
				mes $NPC_Glyph$ ;
				mes "雕文的開發等即是有限制的, " ;
				mes "未轉生 30 / 60 / 90 可以開啟" ;
				mes "一至三枚的初階雕文。" ;
				next ;
				mes $NPC_Glyph$ ;
				mes "轉生 30 / 60 / 90 可以開啟" ;
				mes "一至三枚的極效雕文。" ;
				next ;
				callsub Teaching ;
				close ;
			case 3:
				mes $NPC_Glyph$ ;
				mes "雕文的效果是很多樣化的, " ;
				mes "在道具的說明都會有詳細介紹, " ;
				mes "如果你需要雕文的效果介紹, 我可以提供給你。" ;
				next ;
				callsub Teaching ;
				close ;
			case 4:
				mes $NPC_Glyph$ ;
				mes "是沒有職業限制的, 由於仙境傳說本身的技能擴充非常廣, " ;
				mes "多項裝備、道具捲軸可以習得各種技能, " ;
				mes "因此任何雕文都可以讓每種職業使用。" ;
				mes "※但觸發雕文效果前提必須習得該技能" ;
				next ;
				callsub Teaching ;
				close ;

		} // switch
	close ;

}


function	script	Glyph_Effect	{

	// Glyph_Primer 初階雕文
	for ( set .@i, 1 ; .@i <= 3 ; set .@i, .@i + 1 ) {
		if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "" ) ; // 沒有設置雕文

		// Job - Novice series skills
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "基本技能雕文" ) { // All Status + 1
			if ( getskilllv( "NV_BASIC" ) ) { bonus bStr, 1 ; bonus bAgi, 1 ; bonus bVit, 1 ; bonus bInt, 1 ; bonus bDex, 1 ; bonus bLuk, 1 ; } }

		// Job - Swordman series skills
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "單手劍使用熟練度雕文" ) { // Atk + 0.5 * SkillLv
			if ( getskilllv( "SM_SWORD" ) ) bonus bAtk, getskilllv( "SM_SWORD" ) * 5 / 10 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "雙手劍使用熟練度雕文" ) { // Atk + 0.5 * SkillLv
			if ( getskilllv( "SM_TWOHAND" ) ) bonus bAtk, getskilllv( "SM_TWOHAND" ) * 5 / 10 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "快速回復雕文" ) { // 增加 HP恢復道具效果 10%、受到治癒術治療量提升 3%
			if ( getskilllv( "SM_RECOVERY" ) ) { bonus bAddItemHealRate, 10 ; bonus2 bSkillHeal2, "AL_HEAL", 3 ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "狂擊雕文" ) { // 狂擊傷害提高 25%, 狂擊帶有擊退 3格效果
			if ( getskilllv( "SM_BASH" ) ) { bonus2 bSkillAtk, "SM_BASH", 25 ; bonus2 bAddSkillBlow, "SM_BASH", 3 ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "挑釁雕文" ) { // Luk + 0.5 * SkillLv
			if ( getskilllv( "SM_PROVOKE" ) ) bonus bLuk, getskilllv( "SM_PROVOKE" ) * 5 / 10 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "怒爆雕文" ) { // 使用技能 10秒內獲得鎧甲火屬性
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "霸體雕文" ) { // Vit、Def、Mdef + 0.2 * SkillLv
			 }

		// Job - Mage series skills
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "禪心雕文" ) { // 技能延遲 - 0.3 * SkillLv %
			if ( getskilllv( "MG_SRECOVERY" ) ) bonus bDelayRate, - getskilllv( "MG_SRECOVERY" ) * 3 / 10 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "火狩雕文" ) { // 火狩尋敵範圍上升、使用火狩 15秒內獲得火屬性抵禦 10%
			bonus5 bAutoSpellOnSkill, "MG_SIGHT", "NPC_WIDESIGHT", 1, 1000, 1 ; autobonus3 "{ bonus2 bSubEle, Ele_Fire, 10 ; }", 1000, 15000, "MG_SIGHT", "{ specialeffect2 225 ; }" ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "火箭術雕文" ) { // 火箭術傷害增加 10%、火箭術詠唱時間縮短 10%
			bonus2 bSkillAtk, "MG_FIREBOLT", 10 ; bonus2 bCastrate, "MG_FIREBOLT", -10 ; autobonus3 "{ }", 1000, 10, "MG_FIREBOLT", "{ specialeffect2 958 ; }" ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "冰箭術雕文" ) { // 冰箭術傷害增加 10%、冰箭術傷詠唱時間縮短 10%
			bonus2 bSkillAtk, "MG_COLDBOLT", 10 ; bonus2 bCastrate, "MG_COLDBOLT", -10 ; autobonus3 "{ }", 1000, 10, "MG_COLDBOLT", "{ specialeffect2 954 ; }" ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "雷擊術雕文" ) { // 雷擊術傷害增加 10%、雷擊術詠唱時間縮短 10%
			bonus2 bSkillAtk, "MG_LIGHTNINGBOLT", 10 ; bonus2 bCastrate, "MG_LIGHTNINGBOLT", -10 ; autobonus3 "{ }", 1000, 10, "MG_LIGHTNINGBOLT", "{ specialeffect2 948 ; }" ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "聖靈召喚雕文" ) { // 聖靈召喚傷害增加 20%、聖靈召喚詠唱時間縮短 20%
			bonus2 bSkillAtk, "MG_SOULSTRIKE", 20 ; bonus2 bCastrate, "MG_SOULSTRIKE", -20 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "火球術雕文" ) { // 強化所有火屬性技能 + 0.2 * SkillLv %
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "冰凍術雕文" ) { // 強化所有水屬性技能 + 0.2 * SkillLv %
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "雷爆術雕文" ) { // 強化所有風屬性技能 + 0.2 * SkillLv %
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "石化術雕文" ) { // 強化所有地屬性技能 + 0.2 * SkillLv %
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "心靈爆破雕文" ) { // 強化所有念屬性技能 + 0.4 * SkillLv %
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "火焰之壁雕文" ) { // 附加著火效果
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "暗之障壁雕文" ) { // MDef + 0.5 * SkillLv
			if ( getskilllv( "MG_SAFETYWALL" ) ) bonus bMdef, getskilllv( "MG_SAFETYWALL" ) * 5 / 10 ; }

		// Job - Archer series skills
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "鶚梟之眼雕文" ) { // Dex + 0.3 * SkillLv
			if ( getskilllv( "AC_OWL" ) ) bonus bMdef, getskilllv( "AC_OWL" ) * 3 / 10 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "蒼鷹之眼雕文" ) { // 所有武器攻擊距離增加 1
			if ( getskilllv( "AC_VULTURE" ) ) bonus bAtkRange, 1; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "心神凝聚雕文" ) { // 使用心神凝聚 移動速度額外增加 5%, 能夠與其他移動速度技能並存, 持續時間 100秒
			if ( getskilllv( "AC_CONCENTRATION" ) ) autobonus3 "{ bonus bSpeedAddRate, 5 ; }", 1000, 100000, "AC_CONCENTRATION", "{ specialeffect2 519 ; }" ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "二連矢雕文" ) { // 使用二連矢獲得 Dex增加 5的狀態, 持續時間 3秒
			if ( getskilllv( "AC_DOUBLE" ) ) autobonus3 "{ bonus bDex, 5 ; }", 1000, 3000, "AC_DOUBLE", "{ specialeffect2 649 ; }" ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "箭雨雕文" ) { // 箭雨等級 5以上, 1% 機率普通攻擊 1秒濺射狀態 3*3, 每提升 1等級機率上升 0.5%
			if ( getskilllv( "AC_SHOWER" ) >= 5 ) autobonus "{ bonus bSplashRange, 1 ; }", 10 + ( getskilllv( "AC_SHOWER" ) - 5 ) * 5, 1000, BF_WEAPON, "{ }" ; }

		// Job - Acolyte series skills
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "天使之護雕文" ) { // Def、MDef + 0.3 * SkillLv
			if ( getskilllv( "AL_DP" ) ) { bonus bDef, getskilllv( "AL_DP" ) * 3 / 10 ; bonus bMdef, getskilllv( "AL_DP" ) * 3 / 10 ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "天使之擊雕文" ) { // Atk、MAtk + 0.2 * SkillLv
			if ( getskilllv( "AL_DEMONBANE" ) ) { bonus bAtk, getskilllv( "AL_DEMONBANE" ) * 2 / 10 ; bonus bMatk, getskilllv( "AL_DEMONBANE" ) * 2 / 10 ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "光獵雕文" ) { // 5% 機率使用魔法技能 10秒內獲得全屬性抵禦 3%
			if ( getskilllv( "AL_RUWACH" ) ) autobonus "{ bonus2 bSubEle, Ele_Neutral, 3; bonus2 bSubEle, Ele_Water, 3 ; bonus2 bSubEle, Ele_Earth, 3; bonus2 bSubEle, Ele_Fire, 3 ; bonus2 bSubEle, Ele_Wind, 3 ; bonus2 bSubEle, Ele_Poison, 3 ; bonus2 bSubEle, Ele_Holy, 3; bonus2 bSubEle, Ele_Dark, 3; bonus2 bSubEle, Ele_Ghost, 3 ; bonus2 bSubEle, Ele_Undead, 3 ; }", 50, 10000, BF_MAGIC, "{ specialeffect2 542 ; }"; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "光之障壁雕文" ) { // 永久受到遠距離傷害減少 3%, 光之障壁 SP消耗增加 10
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "瞬間移動雕文" ) { // Agi + 3、Flee + 3
			if ( getskilllv( "AL_TELEPORT" ) ) bonus bAgi, 3 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "傳送之陣雕文" ) { // 施展傳送之陣時, 詠唱不可被中斷
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "治癒術雕文" ) { // 治癒術恢復量 + 0.5 * SkillLv
			bonus2 bSkillHeal, "AL_HEAL", getskilllv( "AL_HEAL" ) * 5 / 100 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "加速術雕文" ) { // 加速術效果增加 Flee + ( 1 * SkillLv ), 60秒
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "緩速術雕文" ) { // 使用緩速術時, 自身的移動速度增加 30%, 持續2秒
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "天使之淚雕文" ) { // Int + 3、使用天使之淚 50%機率獲得 2瓶聖水
			if ( getskilllv( "AL_HOLYWATER" ) ) { bonus bInt, 3 ; autobonus3 "{ }", 1000, 10, "AL_HOLYWATER", "{ specialeffect2 9 ; if ( rand( 0, 1 ) ) getitem 523, 1 ; }" ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "天使之光雕文" ) { // 使用天使之光時, 5秒內獲得鎧甲聖屬性
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "天使之障壁雕文" ) { // 來自不死系、惡魔系傷害減少 0.5 * SkillLv %
			if ( getskilllv( "AL_ANGELUS" ) ) bonus2 bSubRace, RC_Undead, getskilllv( "AL_ANGELUS" ) * 5 / 10 ; bonus2 bSubRace, RC_Demon, getskilllv( "AL_ANGELUS" ) * 5 / 10  ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "天使之賜福雕文" ) { // 30% 機率增加施放者的 All Status + 1, 15秒
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "治療術雕文" ) { // 使用治療術時, 100%機率聯繫技能(不需消耗SP), 對目標施展等級 5的 治癒術
			if ( getskilllv( "AL_CURE" ) ) bonus5 bAutoSpellOnSkill, "AL_CURE", "AL_HEAL", 5, 1000, 0 ; }

		// Job - Merchant series skills
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "負重量上升雕文" ) { // Str + 0.3 * SkillLv、額外獲得 50 * SkillLv 負重量
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "低價買進雕文" ) { // 低價買進 技能等級上升 1
			skill "MC_DISCOUNT", getskilllv( "MC_DISCOUNT" ) + 1 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "高價賣出雕文" ) { // 高價賣出 技能等級上升 1
			skill "MC_OVERCHARGE", getskilllv( "MC_OVERCHARGE" ) + 1 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "手推車使用雕文" ) { // 移動速度增加 0.5 * SkillLv %
			bonus bSpeedRate, getskilllv( "MC_PUSHCART" ) * 5 / 10 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "物品鑒定雕文" ) { // Dex、Int、Luk + 3
			if ( getskilllv( "MC_IDENTIFY" ) ) { bonus bDex, 3 ; bonus bInt, 3 ; bonus bLuk, 3 ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "露天商店雕文" ) { // 增加 NPC好感度 的上升速度
			 }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "金錢攻擊雕文" ) { // 金錢攻擊傷害增加 30%, 減少 100 的 Zeny消耗
			if ( getskilllv( "MC_MAMMONITE" ) ) { bonus2 bSkillAtk, "MC_MAMMONITE", 30 ; autobonus3 "{ }", 1000, 10, "MC_MAMMONITE", "{ specialeffect2 518 ; set Zeny, Zeny + 100 ; }" ; } }

		// Job - Theif series skills
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "二刀連擊雕文" ) { // 裝備短劍時, Atk增加 1 * SkillLv
			if ( getskilllv( "TF_DOUBLE" ) ) bonus2 bWeaponAtk, 1, getskilllv( "TF_DOUBLE" ) ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "殘影雕文" ) { // 完全迴避 + 0.2 * SkillLv
			if ( getskilllv( "TF_MISS" ) ) { bonus bFlee2, getskilllv( "TF_MISS" ) * 2 / 10 ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "偷竊雕文" ) { // Dex + 3, 偷竊成功機率上升 1 * SkillLv %
			if ( getskilllv( "TF_DETOXIFY" ) ) { bonus bDex, 3 ; bonus bAddStealRate, getskilllv( "TF_STEAL" ) ; } }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "隱匿雕文" ) { // 10% 機率受到物理攻擊傷害, 使用已習得等級的 隱匿
			if ( getskilllv( "TF_HIDING" ) ) bonus3 bAutoSpellWhenHit, "TF_HIDING", getskilllv( "TF_HIDING" ), 100 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "施毒雕文" ) { // 施毒傷害增加 30%
			bonus2 bSkillAtk, "TF_POISON", 30 ; }
		else if ( getd( "Glyph_Primer_0" + .@i + "$" ) == "解毒雕文" ) { // Vit + 3, 使用解毒自身恢復 100HP
			if ( getskilllv( "TF_DETOXIFY" ) ) { bonus bVit, 3 ; autobonus3 "{ }", 1000, 10, "TF_DETOXIFY", "{ specialeffect2 331 ; Heal 100, 0 ; }" ; } }
	} // for

}