/************************************************************\
					EAthena Script
**************************************************************
【名    称】自动挂机
【类    型】辅助工具
【版    本】1.0
【版权所有】泠然
【服 务 端】99MAXEA v8.13.1
【制作人员】
    策划：泠然
    编程：泠然
    文饰：泠然
    制作：泠然
    测试：泠然
【简要说明】
	本脚本为 EAthena 服务端用挂机脚本，适合自立服使用。
	功能：1、自动攻击周围敌人（普通攻击）
		  2、自动吃觉醒药水（身上得有）
		  3、离开挂机地图，自动传回（注释掉了）开启的话把这3行注释取消
		  //if(strcharinfo(3)!=@AutoAtkMap$){
		  //	warp @AutoAtkMap$,0,0;
		  //}
		  4、自动使用治愈术，加速术，天赐，心神凝聚，圣母之折福。
		  5、需要配合自动拾取功能@autoloot，本身没有拾取功能。
		  6、每30秒杀怪后会飞一次，如果150秒没有杀怪也会飞。
	测试：没有经过太多测试。挂了几个号2天没出问题。仅供学习。
	
【使用方法】
	1、将脚本加载到服务器中。
	2、在Item_db中 的某个物品上加入脚本AutoAttack。
	例如：50007,auto_attack,自动挂机,11,0,,0,,,,,0xFFFFFFFF,7,2,,,,,,{ callfunc "AutoAttack"; },{},{}
【更新日志】
    2016-04-23    1.0.0    创建脚本
【错误回报】论坛反馈
【相关人物变量】
\************************************************************/
-	script	CheckStatus	-1,{
OnNPCKillEvent:
	if(@startAutoAtk==0){
		end;
	}
	if(@allowFly>=1){//如果有瞬间移动技能，则不消耗苍蝇翅膀。
		if(getskilllv(26)){
			warp "Random",0,0;
		}else if(countitem(601)>0){
			delitem 601,1;
			//itemskill "AL_TELEPORT",1;
			warp "Random",0,0;
		}
		set @allowFly,0;
	}
end;

OnPCDieEvent://死亡停止挂机
	if(@startAutoAtk==1){
		set @startAutoAtk,0;
		atcommand "@autoattack";
	}
end;

OnCheckUp://检查状态
	if(@startAutoAtk==0){
		end;
	}
	if(!getstatus(32)){//加速术
		if(getskilllv(29)&&readparam(7)>=15+getskilllv(29)*3){
			Sp=Sp-15-getskilllv(29)*3;
			sc_start 32,240000,getskilllv(29);
			skilleffect 29,0;
		}
	}
	if(!getstatus(30)){//天赐
		if(getskilllv(34)&&readparam(7)>=24+getskilllv(34)*4){
			Sp=Sp-24-getskilllv(34)*4;
			sc_start 30,240000,getskilllv(34);
			skilleffect 34,0;
		}
	}
	if(!getstatus(23)){//心神凝聚
		if(getskilllv(45)&&readparam(7)>=20+getskilllv(45)*5){
			Sp=Sp-20-getskilllv(45)*5;
			sc_start 23,240000,getskilllv(45);
			skilleffect 45,0;
		}
	}
	if(!getstatus(115)){//圣母之祈福
		if(getskilllv(361)&&readparam(7)>=10+getskilllv(361)*10){
			Sp=Sp-10-getskilllv(361)*10;
			sc_start 115,100000,getskilllv(361);
			skilleffect 361,0;
		}
	}
	if(!getstatus(56)&&BaseClass!=4&&Class!=19&&Class!=20&&Class!=4020&&Class!=4021){//觉醒药水 服饰系列和 舞娘诗人无法使用
		if(countitem(656)>0){
			delitem 656,1;
			consumeitem(656);
		}
	}
	set @allowFly,@allowFly+1;
	if(@allowFly>5){
		set @allowFly,0;
		if(getskilllv(26)){
			warp "Random",0,0;
		}else if(countitem(601)>0){
			delitem 601,1;
			warp "Random",0,0;
		}
	}
	//if(strcharinfo(3)!=@AutoAtkMap$){
	//	warp @AutoAtkMap$,0,0;
	//}
	addtimer 30000,"CheckStatus::OnCheckUp";//30秒检查一次状态
end;

OnCheckUpHp://自动治愈术回血
	if(@startAutoAtk==0){
		end;
	}
	if(readparam(5)*100/readparam(6)<30){
		if(getskilllv(28)&&readparam(7)>=10+getskilllv(28)*3){
			//dispbottom readparam(bInt)*2+(readparam(bInt)/7)*(readparam(bInt)/7)+(readparam(bInt)/5)*(readparam(bInt)/5);
			set @healNumber,(readparam(11)+readparam(16))/5*30*(getskilllv(28)/10)*(1+0)+readparam(bInt)*2+(readparam(bInt)/7)*(readparam(bInt)/7)+(readparam(bInt)/5)*(readparam(bInt)/5);
			//治愈术恢复公式(BaseLv+INT)/5*30*(SLv/10)*(1+Mod%)+MATK
			heal @healNumber,-10-getskilllv(28)*3; //加血扣SP
			skilleffect 28,@healNumber;//显示出来
		}
	}
	addtimer 1500,"CheckStatus::OnCheckUpHp";//1.5秒检查一次血量
end;
}

function	script	AutoAttack	{
	mes "[挂机系统]";
	mes "1、角色只会进行普通攻击";
	mes "2、自动使用觉醒药水和辅助技能(治愈术、天赐等)";
	mes "3、如携带苍蝇翅膀或瞬移技能可自动寻怪";
	//mes "4、挂机开始的地图会记录，如果到别的地图会自动传回";
	switch(select(@startAutoAtk==0?"开启":"关闭")) {
		case 1:
			if(@startAutoAtk==0){
				set @AutoAtkMap$,strcharinfo(3);	//记录挂机地图
				set @startAutoAtk,1;
				atcommand "@autoattack";
				addtimer 1000,"CheckStatus::OnCheckUp";
				if(getskilllv(28)){
					addtimer 1000,"CheckStatus::OnCheckUpHp";
				}
				close;
			}else{
				set @startAutoAtk,0;
				atcommand "@autoattack";
				close;
			}
		break;
		case 2:
			addtimer 1000,"CheckStatus::OnCheckUp";
			addtimer 1000,"CheckStatus::OnCheckUpHp";
			close;
		break;
	}
	
}